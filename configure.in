AC_INIT(fscd, 0.90, [http://fjbtndrv.sourceforge.net])
AC_PREREQ([2.57])
AM_INIT_AUTOMAKE([gnu 1.9])

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_CPP
AC_GNU_SOURCE
AC_PROG_INSTALL

AC_HEADER_STDC

AM_ICONV
AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION([0.16.1])

PKG_CHECK_MODULES(X11, [x11 xi xext xtst xrandr])
AC_SUBST(X11_CFLAGS)
AC_SUBST(X11_LIBS)

PKG_CHECK_MODULES(HAL, [hal >= 0.5.8])
AC_SUBST(HAL_CFLAGS)
AC_SUBST(HAL_LIBS)

AC_ARG_ENABLE([wacom],
  AS_HELP_STRING([--disable-wacom],
    [Disable WACOM support (Default: auto)]),
  [enable_wacom=$enableval], [enable_wacom=auto])
if test x$enable_wacom != xno; then
  AC_CHECK_LIB([wacomcfg], [WacomConfigInit], [has_wacom=yes], [has_wacom=no])
  if test x$enable_wacom = xyes -a x$has_wacom != xyes; then
    AC_MSG_ERROR([wacom library not found])
  fi
  enable_wacom=$has_wacom
fi
if test x$enable_wacom = xyes; then
  AC_DEFINE(ENABLE_WACOM, [], [enable wacom support])
  AC_SUBST(LIBWACOMCFG_CFLAGS)
  LIBWACOMCFG_CFLAGS=
  AC_SUBST(LIBWACOMCFG_LIBS)
  LIBWACOMCFG_LIBS=-lwacomcfg
fi

AC_ARG_ENABLE([xosd],
  AS_HELP_STRING([--disable-xosd],
    [Disable OSD support (Default: auto)]),
  [enable_xosd=$enableval], [enable_xosd=auto])
if test x$enable_xosd != xno; then
  AM_PATH_LIBXOSD([has_xosd=yes], [has_xosd=no])
  if test x$enable_xosd = xyes -a x$has_xosd != xyes; then
    AC_MSG_ERROR([xosd library not found])
  fi
  enable_xosd=$has_xosd
fi
if test x$enable_xosd = xyes; then
  AC_DEFINE(ENABLE_XOSD, [], [enable osd support])
fi

AC_ARG_ENABLE([debug],
  AS_HELP_STRING([--enable-debug],
    [Enable debug output (Default: disabled)]),
  [enable_debug=$enableval], [enable_debug=no])
if test x$enable_debug = xyes; then
  AC_DEFINE(DEBUG, [], [debug output])
fi


AC_ARG_WITH(repeat-rate,
  [  --with-repeat-rate=rate     repeat rate for autorepeat (Default: 16)],
  rep_rate=$withval,
  rep_rate=16)
if test x$rep_rate != xno; then
  AC_DEFINE_UNQUOTED(DEFAULT_REP_RATE, [$rep_rate], [autorepeat rate])
fi

AC_ARG_WITH(repeat-delay,
  [  --with-repeat-delay=msec    delay befor autorepeat (Default: 500)],
  rep_delay=$withval,
  rep_delay=500)
if test x$rep_rate != xno; then
  if test x$rep_delay = xno; then
    rep_delay=0
  fi
  AC_DEFINE_UNQUOTED(DEFAULT_REP_DELAY, [$rep_delay], [autorepeat delay])
fi

AC_ARG_WITH(sticky-timeout,
  [  --with-sticky-timeout=msec  timeout for sticky modification keys (Default: 3000)],
  sticky_timeout=$withval,
  sticky_timeout=3000)
if test x$sticky_timeout != xno; then
  AC_DEFINE_UNQUOTED(DEFAULT_STICKY_TIMEOUT, [$sticky_timeout], [sticky timeout])
fi


AC_CONFIG_FILES([
  Makefile
  src/Makefile
  po/Makefile.in
  contrib/Makefile
])

AC_OUTPUT

echo "

  F S C D

  Copyright (C) 2006-2007  Robert Gerlach

  This program is free software; you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation; either version 2 of the License, or
  (at your option) any later version.

  See file COPYING for more details.


        prefix:         ${prefix}
        wacom support:  ${enable_wacom}
        osd support:    ${enable_xosd}

  Type 'make' to build and then 'sudo make install' to install fscd.
"
